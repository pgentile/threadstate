allprojects {

    group 'org.example'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    apply plugin: 'idea'

}

/**
 * Plugin Java pour partager facilement la configuration du build entre plusieurs projets
 */
class BaseJavaProjectPlugin implements Plugin<Project> {

    @Override
    void apply(Project project) {
        project.pluginManager.apply 'java'

        project.sourceCompatibility = 1.8

        // Versions des dépendances du projet
        project.ext.versions = [
            spring : '4.2.3.RELEASE',
            slf4j  : '1.7.13',
            junit  : '4.12',
            mockito: '1.10.19',
            assertj: '3.2.0',
        ]

        project.dependencies {

            // Logs
            compile "org.slf4j:slf4j-api:${project.versions.slf4j}"

            // Tests unitaires
            testCompile "junit:junit:${project.versions.junit}"
            testCompile "org.assertj:assertj-core:${project.versions.assertj}"
            testCompile "org.mockito:mockito-core:${project.versions.mockito}"

        }

        // Remplacer définitivement commons-logging par slf4j
        project.configurations.all {

            resolutionStrategy.dependencySubstitution {
                substitute module('commons-logging:commons-logging') with module("org.slf4j:jcl-over-slf4j:${project.versions.slf4j}")
            }
        }

        project.afterEvaluate {

            // Config du compilateur Java
            project.tasks.withType(JavaCompile) {
                options.encoding = 'UTF-8'
            }

        }

    }

}


subprojects {

    apply plugin: BaseJavaProjectPlugin

}

project("${rootProject.name}-spring") {

    dependencies {
        compile project(":${rootProject.name}-core")
        compile "org.springframework:spring-context:${versions.spring}"
        compile "org.springframework:spring-web:${versions.spring}"
    }

}

project("${rootProject.name}-slf4j") {

    dependencies {
        compile project(":${rootProject.name}-core")
        testCompile "org.slf4j:slf4j-simple:${project.versions.slf4j}"
    }

}

project("${rootProject.name}-examples") {

    dependencies {
        compile project(":${rootProject.name}-core")
        compile project(":${rootProject.name}-slf4j")
        compile project(":${rootProject.name}-spring")
        compile 'org.springframework.boot:spring-boot-starter-jersey:1.3.0.RELEASE'
    }

}



